apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin('com.android.library')) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                if (project.plugins.findPlugin('com.android.library')) {
                    from components.release
                } else {
                    from components.java
                }

                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'Android permissions as suspend functions'
                    url = 'https://github.com/alexstyl/warden'
                    licenses {
                        license {
                            name = 'Warden License'
                            url = 'https://github.com/alexstyl/warden/blob/main/LICENSE'
                        }
                    }
                    developers {
                        developer {
                            id = 'alexstyl'
                            name = 'Alex Styl'
                            email = 'alex@alexstyl.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/alexstyl/warden.git'
                        developerConnection = 'scm:git:ssh://github.com/alexstyl/warden.git'
                        url = 'https://github.com/alexstyl/warden/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}